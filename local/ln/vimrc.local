" Tips and Tricks
"
" Search and Replace:
"   1. Change the currently searched for item by typing cgnReplacement<Esc>, repeat with .

" Change text width to 120 which is a more sensible default
set textwidth=120
" Allows you to open a new buffer when there are unwritten changes to the current buffer
set hidden
" Set search to always highlight
set hlsearch

set tags^=./.git/tags;

" Change color scheme to dark solarized if bundles is available {{{
if filereadable(expand("~/.vimrc.bundles"))
  " On remote systems that run tmux not setting the background seems to cause issues with the margins
  " appearing to be a yellow/white color, but on non-remote systems running tmux setting a background
  " just causes poor performance. With this hopefully we only set it when necessary.
  if $TERM == "screen-256color" && !empty($SSH_TTY)
    set background=dark
  endif
  " Tells solarized to use the default terminal background instead of it's own (speeds up terminal behavior)
  let g:solarized_termtrans=1
  colorscheme solarized
  "highlight NonText guibg=#060606
  "highlight Folded  guibg=#0A0A0A guifg=#9090D0
endif
" }}}

" Various mappings for CTRL+P {{{

" Search git directories faster by using silver searcher or ignoring git directories
let g:ctrlp_use_caching = 0

" When selecting multiple files, don't open any as splits (0), but instead open them as buffers (i)
let g:ctrlp_open_multiple_files = '0i'

" r chooses the nearest ancestor that contains a .git, .hg, .svn, or .bzr directory
" a the directory of the current file if the working directory of CTRLP isn't a direct ancestor of the file
let g:ctrlp_working_path_mode = 'ra'

" The following checks to make sure that the silver-searcher binary is available, if it is, use it for searching
" everywhere, not just in Ctrp-P (that's what grepprg and grepformat do)
if executable('rg')
    set grepprg=rg\ --vimgrep\ $*
    set grepformat=%f:%l:%c:%m

    let g:ctrlp_user_command = 'rg %s --files --color=never --glob ""'
elseif executable('ag')
    set grepprg=ag\ --vimgrep\ $*
    set grepformat=%f:%l:%c:%m

    let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
else
  let g:ctrlp_user_command = ['.git', 'cd %s && git ls-files . -co --exclude-standard', 'find %s -type f']
  " Commenting this out for now because it will conflict with my other set of prompt matchings above
  "let g:ctrlp_prompt_mappings = {
  "  \ 'AcceptSelection("e")': ['<space>', '<cr>', '<2-LeftMouse>'],
  "  \ }
endif
" }}}

" Plugin Configurations - Various items for NerdTree, scratch buffers, airline, etc {{{

" Settings for NerdTree so that it autocloses if it's the last window opened
augroup nerdtree
  autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTreeType") && b:NERDTreeType == "primary") | q | endif
augroup END

" If the any of the scratch buffers (opened in the functions below) is the last window open, then close it and exit vim
augroup scratchbuffer
  autocmd bufenter * if (winnr("$") == 1 && exists("b:scratch")) | q | endif
augroup END

" Settings for Airline
" Show buffers as well as tabs
let g:airline#extensions#tabline#enabled = 1
" Allow powerline fonts that pretty print the bars at the top and bottom
let g:airline_powerline_fonts = 1
" Enable/Disable showing a summary of changed hunks under source control
let g:airline#extensions#hunks#enabled=1
" Show branches in Airline
let g:airline#extensions#branch#enabled=1
" Airline theme that matches well with solarized theme
let g:airline_theme="papercolor"
" Show vim-obsession session tracking status in airline (when tracking display [TRK SESS] in lower right)
" In order to restore, use either vim -S or :source
let g:airline_section_z = airline#section#create(['%{ObsessionStatus(''[TRK SESS]'', '''')}', 'windowswap', '%3p%% ', 'linenr', ':%3v '])

" Gitgutter settings
" Set SignColumn highight group to same as LineNr so that it doesn't turn grey
highlight! link SignColumn LineNr

" Settings for jedi-vim
" Don't start auto-completing on . (if you still need auto-completion use Ctrl-Space)
let g:jedi#popup_on_dot = 0

" Settings for tagbar
" When you toggle the tagbar, automatically give it focus
let g:tagbar_autofocus = 1
" Place the tag bar on the left instead of the right side of the window
let g:tagbar_left = 1
" Tagbar settings for Go applications
let g:tagbar_type_go = {
  \ 'ctagstype' : 'go',
  \ 'kinds'     : [
    \ 'p:package',
    \ 'i:imports:1',
    \ 'c:constants',
    \ 'v:variables',
    \ 't:types',
    \ 'n:interfaces',
    \ 'w:fields',
    \ 'e:embedded',
    \ 'm:methods',
    \ 'r:constructor',
    \ 'f:functions'
  \ ],
  \ 'sro' : '.',
  \ 'kind2scope' : {
    \ 't' : 'ctype',
    \ 'n' : 'ntype'
  \ },
  \ 'scope2kind' : {
    \ 'ctype' : 't',
    \ 'ntype' : 'n'
  \ },
  \ 'ctagsbin'  : 'gotags',
  \ 'ctagsargs' : '-sort -silent'
\ }

" }}}

" File type based autocmd's (e.g. python width 120, tab expansion for python, sh, etc, upconf and json settings {{{
augroup filetypes
  " Setting a different indentation for Python so that we get more than 80 characters in a column
  autocmd Filetype python set textwidth=120

  " Set different indent styles for different file types
  autocmd Filetype python setlocal et ts=4 sw=4 sts=4 nowrap nofoldenable
  autocmd Filetype sh setlocal ts=4 sw=4 sts=4 noexpandtab nowrap
  autocmd Filetype ebuild setlocal ts=4 sw=4 sts=4 noexpandtab nowrap
  autocmd Filetype java setlocal ts=4 sw=4 sts=4

  " Set file type to upconf template when it has deploy/config in the path
  autocmd BufRead,BufNewFile */deploy/config/* set filetype=upconf-template

  " Treat .input files the same as JSON
  autocmd BufRead,BufNewFile *.input set filetype=json

  " Treat .confd and .initd files the same as shell
  autocmd BufRead,BufNewFile *.confd set filetype=sh
  autocmd BufRead,BufNewFile *.initd set filetype=sh
augroup END

" }}}

" Various whitespace highlighting, autocmds for EOL highlights {{{
" Setup highlights to be used in autocmds and the functions that autocmds call
highlight ExtraWhitespace ctermbg=red guibg=red
highlight WhiteSpaceBol guifg=darkgrey ctermfg=darkgrey
" These two numbers are the same term color that my solarized terminal uses and can be used to effectively hide
" characters by setting the foreground and background to the same as my terminal
highlight WhiteSpaceMol ctermfg=8 ctermbg=8

" Used in the ToggleLeadingSpace function below, used to indicate that when we open files we don't want them to show
" leading spaces initially
let g:show_leading_space = 0

augroup whitespace
  " Upon buffer read or new buffer setup our listchars settings
  autocmd BufRead,BufNewFile * call SetupLCS()

  " This can be used just in case future colorscheme commands attempt to clear our manually added highlights, this adds
  " them back directly after any colorscheme changes. In practice I haven't found this necessary, commentting for now
  "autocmd ColorScheme * highlight ExtraWhitespace ctermbg=red guibg=red
  "autocmd ColorScheme * highlight WhiteSpaceBol guifg=darkgrey ctermfg=darkgrey
  "autocmd ColorScheme * highlight WhiteSpaceMol ctermfg=8 ctermbg=8

  " This shows extra whitespace EXCEPT when we're typing at the end of the line, therefore, we only want this match
  " loaded when we are in insert mode. We'll load this one, and delete the match below when we enter insert mode. We
  " have to add silent! to the beginning of this call because on the first InsertEnter after opening a file 104 won't
  " exist and 103 will already exist. See call matchadd("ExtraWhitespace"... below.
  " matchadd parameters: matchadd(<highlight_name>, <pattern>, <priority_greater_num_means_more_prio>, <id>)
  autocmd InsertEnter * silent! call matchadd("ExtraWhitespace", '\s\+\%#\@<!$', 10, 103)
  " IDs 100 and 101 are created below in section: My functions (toggle solarized background, etc)
  autocmd insertEnter * call matchdelete(104)

  " This always shows whitespace at the end of the line, we use this pattern when we leave insert mode. When we leave
  " insert we'll load this one and delete the match above. No silent is needed as everything is already setup the way it
  " should (unlike above where it might be the first insert of the session) so no errors are thrown.
  autocmd InsertLeave * call matchadd("ExtraWhitespace", '\s\+$', 10, 104)
  " IDs 100 and 101 are created below in section: My functions (toggle solarized background, etc)
  autocmd InsertLeave * call matchdelete(103)

  " Uses the detectindent plugin to try to figure out what indent this file should use if a modeline is not available
  if exists(":DetectIndent")
    " When you can't detect how many spaces make up an indent, prefer 4
    let g:detectindent_preferred_indent    = 4
    " When you can't detect spaces vs. tabs, prefer tabs
    let g:detectindent_preferred_expandtab = 0
    autocmd BufReadPost * :DetectIndent
  endif
augroup END

" Makes it so that you never scroll into the quickfix buffer when using buffernext/bufferprevious commands
augroup qfix
  autocmd!
  autocmd FileType qf set nobuflisted
augroup END

" When we first open VIM we need to have a rule that maches EOL whitespace (before the InsertEnter and InsertLeave
" autocmds above are called) so that we can see whitespace before messing with insertmode.
call matchadd("ExtraWhitespace", '\s\+$', 10, 104)

" }}}

" Python pep8 and pymode configurations {{{
" Add ignores for annoying pep8 guidelines
" E501: Lines should be less than 80 characters
" E265: There should be a space between # and comments
let g:pep8_ignore="E501,E265"
let g:pymode_lint_ignore="E501,E265"

" }}}

" Syntastic specific configurations {{{

" Don't run syntastic check on open
let g:syntastic_check_on_open = 0
" Don't run syntastic check when we're just going to close the window anyway
let g:syntastic_check_on_wq        = 0
" Aggregate errors from multiple checkers instead of stopping
let g:syntastic_aggregate_errors   = 1
" Automatically open the location list when there are errors
let g:syntastic_auto_loc_list = 1
" Have syntastic user better symbols for marking errors and warnings
let g:syntastic_error_symbol = "✗"
let g:syntastic_warning_symbol = "⚠"
" Specific message format for styntastic
let g:syntastic_python_pylint_post_args = '--msg-template="{path}:{line}:{column}:{C}: {msg_id}[{symbol}] {msg}"'
" Enable ruby-lint for ruby files
let g:syntastic_ruby_checkers = ['mri', 'rubylint', 'rubocop']

" }}}

" Use multiple presses of v to expand and ctrl-v to shrink regions {{{
" Actual mappings down in the binding section, see: expand_region_
" Default settings. (NOTE: Remove comments in dictionary before sourcing)
let g:expand_region_text_objects = {
    \ 'iw'  :0,
    \ 'iW'  :0,
    \ 'i"'  :0,
    \ '^o$' :0,
    \ '0o$' :0,
    \ 'i''' :0,
    \ 'i]'  :1,
    \ 'ib'  :1,
    \ 'iB'  :1,
    \ 'il'  :0,
    \ 'ip'  :0,
    \ 'ie'  :0,
    \ }
" }}}

" Persistent undo settings (allows you to get undo back even after closing a file) {{{

if has("persistent_undo")
  let myUndoDir = expand('$HOME/.vim/undodir/')
  call system('mkdir ' . myUndoDir)
  let &undodir = myUndoDir
  set undofile
endif

" }}}

" My functions (toggle solarized background, etc) {{{

" !help! Custom Functions [[[

" !help! RestoreRegister: Prevent replacing copy buffer on visual paste (preserve previous copy item on visual paste)
" hint: makes it so that vp doesn't replace paste buffer
function! RestoreRegister()
  let @" = s:restore_reg
  return ''
endfunction
function! s:Repl()
  let s:restore_reg = @"
  return "p@=RestoreRegister()\<cr>"
endfunction

" !help! MyTogBG: Toggles the background and colorscheme on and off (this helps screens with bad color or copy/paste)
function! MyTogBG()
  redir => s:curr_background
    silent execute "colorscheme"
  redir END
  let s:curr_background = substitute(s:curr_background, '\n', '', '')
  let &background = ( &background == "dark"? "light" : "dark" )
  if s:curr_background =~? ".*default.*"
    exe "colorscheme " . s:background
  else
    exe "colorscheme default"
  endif
  let s:background = s:curr_background
endfunction

" !help! MyTogTW: Toggles the textwidth ruler on the right (useful when you don't want autowrap or copy/paste operations)
function! MyTogTW()
  if &tw
    let s:txtwid = &textwidth
    let s:wrapmarg  = &wrapmargin
    let &textwidth  = 0
    let &wrapmargin = 0
    set colorcolumn=0
  else
    let &textwidth = s:txtwid
    let &wrapmargin = s:wrapmarg
    set colorcolumn=+1
  endif
endfunction

" !help! ToggleCopyable: Toggles all of the above, plus some extra to allow for select based copy/paste operations)
function! ToggleCopyable()
  call MyTogBG()
  set list!
  set number!
  call MyTogTW()
  if exists(":SyntasticToggleMode")
    exe "SyntasticToggleMode"
  endif
  if exists(":GitGutterToggle")
    exe "GitGutterToggle"
  endif
  " We need to re-link the GitGutter SignColumn to LineNr so it doesn't turn grey
  highlight! link SignColumn LineNr
endfunction

" !help! MyGrepCommand: Automatically opening the quickfix pane (handles MiniBufExplorer)
function! MyGrepCommand(...)
  " This actually runs rg/ag because of the greprg setting above
  exe "silent grep " . join(a:000, " ")
  if exists(":MBEClose")
    exe "MBEClose"
  endif
  copen
  redraw!
  if exists(":MBEOpen")
    exe "MBEOpen"
  endif
endfunction

" !help! MyErrorsCommand: Shows and hides errors and focuses the error list (handles MiniBufExplorer)
function! MyErrorsCommand()
  if exists(":MBEClose")
    exe "MBEClose"
  endif
  if exists(":Errors")
    exe "Errors"
  endif
  exe "redraw!"
  if exists(":MBEOpen")
    exe "MBEOpen"
  endif
  if ! empty(getloclist(0))
    exe "wincmd j"
  endif
endfunction

" !help! ExportKeys: Export current key bindings to a vertical split scratch buffer
function! ExportKeys()
  redir! => l:mappings
    silent exe "map"
  redir END
  botright vnew
  silent exe "put =l:mappings"
  setlocal buftype=nofile bufhidden=wipe noswapfile nobuflisted nowrap
  let b:scratch = 1
  1
  " This could be d 4 but then VIM stops to tell me that 4 lines were removed and X number of lines were added
  d
  d
  d
  d
endfunction

" !help! VSplitCurrentBuffer: Split the current buffer and rotate main screen to previous buffer
function! VSplitCurrentBuffer()
  vs
  wincmd h
  bp
endfunction

" !help! SplitCurrentBuffer: Split the current buffer and rotate main screen to previous buffer
function! SplitCurrentBuffer()
  sp
  wincmd h
  bp
endfunction

" !help! s:RunShellCommand: Function to paste the output of a command inside a scratch buffer
function! s:RunShellCommand(cmdline)
  let isfirst = 1
  let words = []
  for word in split(a:cmdline)
    if isfirst
      let isfirst = 0  " don't change first word (shell command)
    else
      if word[0] =~ '\v[%#<]'
        let word = expand(word)
      endif
      let word = shellescape(word, 1)
    endif
    call add(words, word)
  endfor
  let expanded_cmdline = join(words)
  botright vnew
  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile nowrap
  silent execute '$read !'. expanded_cmdline
  let b:scratch = 1
  1
  d
endfunction

" !help! ShowVimCustomizations: Show VIM customizations
function! ShowVimCustomizations()
  call s:RunShellCommand('extract_vim_help')
  set modeline | doautocmd BufRead
endfunction

" This is really just a helper function to the ToggleList option below
function! s:GetBufferList()
  redir =>buflist
  silent! ls!
  redir END
  return buflist
endfunction

" !help! ToggleList: Toggles either the quickfix or location list windows if they are open
function! ToggleList(bufname, pfx)
  let buflist = s:GetBufferList()
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      exec(a:pfx.'close')
      return
    endif
  endfor
  if a:pfx == 'l' && len(getloclist(0)) == 0
      echohl ErrorMsg
      echo "Location List is Empty."
      return
  endif
  let winnr = winnr()
  exec(a:pfx.'open')
  if winnr() != winnr
    wincmd p
  endif
endfunction

" !help! DoPrettyXML: formats the current XML document in a prettified way
function! DoPrettyXML()
  " save the filetype so we can restore it later
  let l:origft = &ft
  set ft=
  " delete the xml header if it exists. This will
  " permit us to surround the document with fake tags
  " without creating invalid xml.
  1s/<?xml .*?>//e
  " insert fake tags around the entire document.
  " This will permit us to pretty-format excerpts of
  " XML that may contain multiple top-level elements.
  0put ='<PrettyXML>'
  $put ='</PrettyXML>'
  silent %!xmllint --format -
  " xmllint will insert an <?xml?> header. it's easy enough to delete
  " if you don't want it.
  " delete the fake tags
  2d
  $d
  " restore the 'normal' indentation, which is one extra level
  " too deep due to the extra tags we wrapped around the document.
  silent %<
  " back to home
  1
  " restore the filetype
  exe "set ft=" . l:origft
endfunction
command! PrettyXML call DoPrettyXML()

" !help! SetupLCS: Sets up initial (normal) listchars in VIM. It is used on start by autocmd above.
function! SetupLCS()
  if has("patch-8.1-0759")
    set listchars=tab:\|->
  else
    set listchars=tab:->
  endif
  set listchars+=nbsp:·,trail:·
  set listchars+=extends:>,precedes:<
endfunction

" !help! ToggleLeadingSpaces: Disables all leading spaces to make it easier to copy/paste from window
function! ToggleLeadingSpaces(...)
  " A one should be passed as the first argument to this function if you don't want to toggle the functionality, but
  " just want it to re-run existing settings instead.
  if a:0 != 1
    if g:show_leading_space == 0
      let g:show_leading_space = 1
    else
      let g:show_leading_space = 0
    endif
  endif
  if g:show_leading_space == 1 " Show leading spaces
    call SetupLCS()
    " Show the space character as a ·
    set listchars+=space:·

    " IDs 103 and 104 along with highlight definitions are described above in the section titled:
    " Various whitespace highlighting, autocmds for EOL highlights

    " This one matches all whitespace and applies the highlight WhiteSpaceMol hides the space listchar above ^^ by
    " making it's foreground and background the same color as my solarized terminal
    call matchadd("WhiteSpaceMol", " ", -5, 100)

    " This one overrides the above match (by giving it a higher priority -4 > -5) and applies the highlight
    " WhiteSpaceBol which shows the space listchar above ^^ by applying a proper foreground and background
    call matchadd("WhiteSpaceBol", '^ \+', -4, 101)

  else " Hide leading spaces

    call SetupLCS()

    " Delete our above matches so that space characters in any position aren't hidden or improperly displayed
    call matchdelete(100)
    call matchdelete(101)
  endif
endfunction
command! ToggleLeadingSpaces call ToggleLeadingSpaces()

" !help! CreateCtagsInGit: Creates a file in .git/tags with all of the tags from the current project in it
function! CreateCtagsInGit()
  if isdirectory("./.git")
    if &ft == 'go'
      silent! !gotags -silent -tag-relative -sort -f ./.git/tags **/*.go
    else
      silent! !ctags -f ./.git/tags &>/dev/null
    endif
    redraw!
  else
    echohl ErrorMsg
    echo "Cannot find .git directory so cannot create ctags"
  endif
endfunction
command! CreateCtags call CreateCtagsInGit()

" !help! Z: cd to recent / frequent directories
function! Z(...)
  let cmd = 'fasd -d -e printf'
  for arg in a:000
    let cmd = cmd . ' ' . arg
  endfor
  let path = system(cmd)
  if isdirectory(path)
    echo path
    exec 'cd ' . path
  endif
endfunction

" !help! V: edit file based on frequent list
function! V(...)
  let cmd = 'fasd -f -e printf'
  for arg in a:000
    let cmd = cmd . ' ' . arg
  endfor
  let path = system(cmd)
  if filereadable(path)
    "echo path
    exe "edit" . path
  endif
endfunction

" !help! ]]]
" }}}

" Custom commands {{{

" !help! Custom Commands [[[
" !help! ToggleBG: Calls function MyTogBG() to toggle the background color
command! ToggleBG :call MyTogBG()
" !help! toggleTextWidth: Calls function MyTogTW() to toggle the text width limitations
command! ToggleTextWidth :call MyTogTW()
" !help! ToggleCopyable: Calls function ToggleCopyable() to toggle copy mode of text
command! ToggleCopyable :call ToggleCopyable()
" !help! AG: Calls function MyGrepCommand to use AG to grep through stuff
command! -nargs=* AG :call MyGrepCommand(<f-args>)
" !help! AG: Calls function MyGrepCommand to use RG to grep through stuff
command! -nargs=* RG :call MyGrepCommand(<f-args>)
" !help! Shell: Calls s:RunShellCommand to show command output in a vertical split
command! -complete=shellcmd -nargs=+ Shell call s:RunShellCommand(<q-args>)
" !help! Z: cd to recent / frequent directories
command! -nargs=* Z :call Z(<f-args>)
" !help! V: edit file based on frequent list
command! -nargs=* V :call V(<f-args>)
" !help! ]]]

" }}}

" Remapping keys using combos and leaders {{{

" Reminder of what stuff means:
" (remap is the default, noremap means that the binding is not recursive, meaning that whatever the value of the binding
" is, it takes that value regardless of other surrounding bindings)
"
" :map  - Normal, Visual, Select, Operator-pending
" :nmap - Normal
" :vmap - Visual and Select
" :smap - Select
" :xmap - Visual
" :omap - Operator-pending
" :map! - Insert and Command-line
" :imap - Insert
" :lmap - Insert, Command-line, Lang-Arg
" :cmap - Command-line

" !help! Key Bindings [[[

" !help! Command Bindings [[[

" !help!  w!!: Allow saving of files as sudo when we forgot to start vim using sudo
cmap w!! w !sudo tee > /dev/null %
" !helm! <Ctrl-g><CR>: Activate search as quick jump with <Ctrl-g><CR> key combo is used at the end of a search pattern
cmap <C-g><CR> <Plug>(SearchAsQuickJump)

" !help! ]]]

" !help! Visual Bindings [[[
" !help! v: Expand selection with multiple presses of v
vmap v <Plug>(expand_region_expand)
" !help! <CTRL-v>: Shrink Selection with multiple presses of CTRL-v
vmap <C-v> <Plug>(expand_region_shrink)
" !help! p: Change visual paste to use the Repl() function described in "Prevent replacing copy buffer..."
vmap <silent> <expr> p <sid>Repl()
" !help! y: Jump to the end of the text you just pasted
vnoremap <silent> y y`]
" !help! p: Jump to the end of the text you just pasted
vnoremap <silent> p p`]
" !help! <Leader-d>: Delete item to the blackhole register (meaning don't overwrite the current buffer)
vnoremap <Leader>d "_d
" For more info on the below see: http://vim.wikia.com/wiki/Copy_or_change_search_hit
" !help! Creates a simple search text object identified as "s" so that you can manipulate search hits (do things like yank, change, etc)
vnoremap <silent> s //e<C-r>=&selection=='exclusive'?'+1':''<CR><CR>
      \:<C-u>call histdel('search',-1)<Bar>let @/=histget('search',-1)<CR>gv
" !help! ]]]

" !help! Normal Bindings [[[
" !help! p: Jump to the end of the text you just pasted
nnoremap <silent> p p`]
" !help! <Leader-dd>: Delete line to the blackhole register (meaning don't overwrite the current buffer)
nnoremap <Leader>dd "_dd
" !help! <Leader-d>: Delete item to the blackhole register (meaning don't overwrite the current buffer)
nnoremap <Leader>d "_d
" !help! <Leader-x>: Delete character to the blackhole register (meaning don't overwrite the current buffer)
nnoremap <Leader>x "_x
" !help! <leader b>: Open/close buffer list and prompt for selection
nnoremap <Leader>b :ls<CR>:b<Space>
" !help! gb: Open/close buffer list and prompt for selection
nnoremap gb :ls<CR>:b<Space>
" !help! <CTRL-b>: Open ctrlp in tag mode for all files under the current working directory
nnoremap <C-b> :CtrlPTag<CR>
" !help! <ALT-p>: Open ctrlp in using a new root
nnoremap <Esc>p :CtrlPCurWD<CR>
" !help! <Leader-s>: Toggle showing/hiding leading spaces
nnoremap <Leader>s :ToggleLeadingSpaces<CR>
" !help! <Leader-B>: Generate ctags in .git directory if it exists
nnoremap <Leader>B :CreateCtags<CR>
" Set tab merge alias to vertically split current tab and the tab to the right (or barring that, to the left)
" To put them back to tabs afterward, type CTRL+w, SHIFT+t
" !help! <CTRL-t>: Open tag list side bar
nnoremap <C-t> :TagbarToggle<CR>
" !help! <Leader-g>: Pre-forms Z change of path command
nnoremap <Leader>g :Z 

" !help! ]]]

" !help! Insert Bindings [[[
" !help! jj: When hit in quick succession same as escape
imap jj <Esc>
" !help! JJ: When hit in quick succession same as escape
imap JJ <Esc>
" !help! ]]]

" !help! Operator Pending Bindings [[[
" For more information see: http://vim.wikia.com/wiki/Copy_or_change_search_hit
" !help! Operator pending mode is entered with "c" or "y" pressing v after executes visual mode and then s after it which is defined above
omap v :normal vs<CR>
" !help! ]]]

" !help! Map Bindings [[[
" !help! gV: Quickly select the text you just pasted
noremap gV `[v`]

" Add control based navigation to insert mode
" !help! <CTRL-g>: Move right one word in any mode
map <C-G> <C-Right>
map! <C-G> <C-Right>
" !help! <CTRL-f>: Move left one word in any mode
map <C-F> <C-Left>
map! <C-F> <C-Left>
" !help! <CTRL-j>: Move to the beginning of the line in any mode
map <C-J> ^
map! <C-J> <Home>
" !help! <CTRL-k>: Move to the end of the line in any mode
map <C-K> $
map! <C-K> <End>

" !help! <CTRL-n>: Go to the previous error location
map <C-n> :lprev<CR>
" !help! <CTRL-m>:Go to the next error location
map <C-m> :lnext<CR>

" Set tab merge alias to vertically split current tab and the tab to the right (or barring that, to the left)
" To put them back to tabs afterward, type CTRL+w, SHIFT+t
" removing in favor of taglist, leaving this here for now for history: map <C-t> <esc>:Tabmerge right<CR>

" Overwrite jedi-vim shortcuts
" !help! Jedi Python Autocomplete and GoTo Mappings [[[
" !help! <CTRL-j> d: Go to command
let g:jedi#goto_command = "<C-j>d"
" !help! <CTRL-j> g: Go to assignment
let g:jedi#goto_assignments_command = "<C-j>g"
let g:jedi#goto_definitions_command = ""
" !help! <CTRL-j> k: Go to documentation
let g:jedi#documentation_command = "<C-j>k"
" !help! <CTRL-j> n: Find usages
let g:jedi#usages_command = "<C-j>n"
" !help! <CTRL-space>: Autocomplete word or phrase
let g:jedi#completions_command = "<C-Space>"
" !help! <CTRL-j> r: Rename item
let g:jedi#rename_command = "<C-j>r"
" !help! ]]]

" Remap next and previous tab to Ctrl-PgDn and Ctrl-PgUp
" !help! <ALT-h>: Go to previous tab
map <Esc>h :tabp<CR>
" !help! <ALT-l>: Go to next tab
map <Esc>l :tabn<CR>
" !help! <ALT-j>: Go to previous buffer
map <Esc>j :bp<CR>
" !help! <ALT-k>: Go to next buffer
map <Esc>k :bn<CR>
" This does the same as the above <Esc>j and <Esc>k, but it is needed for OSX which sends characters instead
map ê :bp<CR>
map ë :bn<CR>

" !help! <leader p>: Toggle paste mode
map <Leader>p :set paste!<CR>
" !help! <leader po>: Paste a partial yank on the line below
map <leader>po :pu<CR>
" !help! <leader pO>: Paste a partial yank on the line above
map <leader>pO :pu!<CR>
" !help! <leader y>: Leader mapping to allow for copying from vim
map <Leader>y :ToggleCopyable<CR>
" !help! <leader w>: Toggle text width on and off
map <Leader>w :ToggleTextWidth<CR>
" !help! <leader l>: Open/Close the location list window (for things like pylint)
map <silent> <leader>l :call ToggleList("Location List", 'l')<CR>
" !help! <leader q>: Open/Close the quickfix window
map <silent> <leader>q :call ToggleList("Quickfix List", 'c')<CR>
" !help! <leader li>: Toggle showing certain whitespace characters
map <Leader>li :set list!<CR>
" !help! <leader n>: Toggle show number
map <Leader>n :set number!<CR>
" !help! <leader r>: Toggle relative line numbering
map <Leader>r :set relativenumber!<CR>
" !help! <leader e>: Shows syntastic errors location window in a way that won't affect MiniBufExplorer
map <Leader>e :call MyErrorsCommand()<CR>
" !help! <leader csd>: Reset the color scheme to the default (in cases with unconfigured terminals)
map <Leader>csd :ToggleBG<CR>
" !help! <leader t>: Map nerdtree to leader+t
map <Leader>t :NERDTreeToggle<CR>
" !help! <leader k>: Search current buffer for item under the current cursor
map <leader>k :RG <cword> %<CR>
" !help! <leader K>: Search the entire working directory for the item under the cursor
map <leader>K :RG <cword><CR>
" !help! <leader u>: Toggle undo tree and then focus the original vim pane again with leader-u
map <leader>u :UndotreeToggle<cr><C-w>l
" !help! <leader S>: Force a Synstastic check
map <leader>S :SyntasticCheck<CR>
" !help! <leader j>: Re-format a JSON document, this allows you to write compactly, but read "prettily"
map <leader>j :%!python -m json.tool<CR>
" !help! <leader X>: Re-format an XML document in a way that is easy to read but may not be syntatically the same
map <leader>X :call DoPrettyXML()<CR>
" !help! <leader map>: Dump all of the current keys bindings/mappings to a file and open it
map <leader>map :call ExportKeys()<CR>
" !help! <leader vs>: Split the current buffer to the other vertical
map <leader>vs :call VSplitCurrentBuffer()<CR>
" !help! <leader hs>: Split the current buffer to the other horizontal
map <leader>hs :call SplitCurrentBuffer()<CR>
" !help! <leader vc>: Open my vim customizations to a vertical split
map <leader>vc :call ShowVimCustomizations()<CR>
" !help! <leader z>: Close current buffer
map <leader>z :bd<CR>
" !help! <leader Z>: Close current window (use instead of :q)
map <leader>Z :close<CR>
" !help! <leader T>: Begin tracking the current vim session with the obsession plugin
map <leader>T :Obsess ~/.vim/session/%:t<CR>
" !help! ]]]

" !help! QuickFix Window Bindings [[[
" !help! <Enter>: Open location in a new buffer
" let g:qfenter_open_map = ['<CR>', '<2-LeftMouse>']
" !help! <CTRL-v>: Open location in a vertical split
let g:qfenter_vopen_map = ['<C-v>']
" !help! <CTRL-Enter> or <CTRL-s> or <CTRL-x>: Open in a horizontal split
let g:qfenter_hopen_map = ['<C-CR>', '<C-s>', '<C-x>']
" !help! <CTRL-t>: Open in a new tab
let g:qfenter_topen_map = ['<C-t>']
" !help! ]]]

" }}}

" vim: fdm=marker ts=2 et
