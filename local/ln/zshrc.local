if [[ $(uname -a) == *"Darwin"* ]]; then
  SED_OPTS='-E'
else
  SED_OPTS='-r'
fi

# Command to start up ssh-agen if there is an SSH key in your profile
if [[ -z ${SSH_AUTH_SOCK} && -z $(pgrep ssh-agent) ]]; then
  eval $(ssh-agent)
fi

# Add any missing SSH identities
SSH_ADD=$(ssh-add -l)
for file in $(ls ${HOME}/.ssh/id*.pub 2> /dev/null); do
  identity=$(echo ${file} | sed ${SED_OPTS} 's/.*(\.ssh.*).pub/\1/g')
  if [[ $SSH_ADD != *${identity}* ]]; then
    echo "Identity didn't exist, adding: ${identity}"
    ssh-add ${identity}
  fi
done

# Changes prompt to include information about Git repos and what zvim mode we're in
export PS1='${SSH_CONNECTION+"%{$fg_bold[green]%}%n@%m:"}%{$fg_bold[blue]%}%~%{$reset_color%}$(git_prompt_info) %#'
source "${HOME}"/dotfiles/local/lib/zsh_vi_status_prompt.sh

# Binds keys appropriately so that special keys like Home, End, and Delete work
# These maps can be created by running "sed -n l" and then typing the HOME, END, INSERT, and DELETE keys
# once you get the output, add it to a new map, then you can either manually or automatically source it.
#source "${HOME}/.keys/00-map"

# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo
typeset -A key

key[Home]=${terminfo[khome]}
key[End]=${terminfo[kend]}
key[Insert]=${terminfo[kich1]}
key[Delete]=${terminfo[kdch1]}
key[Up]=${terminfo[kcuu1]}
key[Down]=${terminfo[kcud1]}
key[Left]=${terminfo[kcub1]}
key[Right]=${terminfo[kcuf1]}
key[PageUp]=${terminfo[kpp]}
key[PageDown]=${terminfo[knp]}

# setup key accordingly
[[ -n "${key[Home]}"    ]]  && bindkey  "${key[Home]}"    beginning-of-line
[[ -n "${key[End]}"     ]]  && bindkey  "${key[End]}"     end-of-line
[[ -n "${key[Insert]}"  ]]  && bindkey  "${key[Insert]}"  overwrite-mode
[[ -n "${key[Delete]}"  ]]  && bindkey  "${key[Delete]}"  delete-char
[[ -n "${key[Up]}"      ]]  && bindkey  "${key[Up]}"      up-line-or-history
[[ -n "${key[Down]}"    ]]  && bindkey  "${key[Down]}"    down-line-or-history
[[ -n "${key[Left]}"    ]]  && bindkey  "${key[Left]}"    backward-char
[[ -n "${key[Right]}"   ]]  && bindkey  "${key[Right]}"   forward-char

# Finally, make sure the terminal is in application mode, when zle is
# active. Only then are the values from $terminfo valid.
function zle-line-init () {
    echoti smkx
}
function zle-line-finish () {
    echoti rmkx
}
zle -N zle-line-init
zle -N zle-line-finish  

# Add local bin file
if [[ -e ${HOME}/bin ]]; then
  export PATH=${HOME}/bin:${PATH}
fi

# Switch to incremental pattern search, plus ability to go forward and backward
bindkey -M viins '^R' history-incremental-pattern-search-backward
bindkey -M vicmd '/' history-incremental-pattern-search-backward
bindkey -M isearch '^R' history-incremental-search-backward
bindkey -M isearch '^F' history-incremental-search-forward
